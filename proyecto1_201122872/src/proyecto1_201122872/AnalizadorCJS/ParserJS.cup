package proyecto1_201122872.AnalizadorCJS;

import proyecto1_201122872.AnalizadorCJS.simbolosCJS;
import java_cup.runtime.*;
import java.io.*;
import java.lang.*;
import java.util.*;
import java.util.ArrayList;
import CJS.ARBOL.EXPRESION.*;
import CJS.objetoBase;
import CJS.TablaSimbolos.*;
import CJS.ARBOL.INSTRUCCIONES.*;
import CJS.ESTRUCURAS_CONTROL.*;
import Funciones.*;


parser code
{:
 /* -------------------------==: DECLARAR EL ARBOL :==--------------------------*/
    //public Nodo Gramaticas;     
    public static String cadena;
	public objetoBase raiz;
	public List<objetoBase>listaSentencias;

	

/* -------------------------==: ERROR SINTACTICO :==--------------------------*/
public void syntax_error(Symbol s){
    System.out.println("Error Sintactico -> lexema: " +s.value+" Linea: " +s.right+ " Columna: "+s.left);
    cadena += "<tr>"
                + "<td><center><b>" + s.value + "</b></center></td>"
                + "<td><center><b>" + s.right + "</b></center></td>"
                + "<td><center><b>" + s.left+ "</b></center></td>"
                + "</tr>";
}

/* ------------------==: ERROR SINTACTICO NO RECUPERABLE :==------------------*/
public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
    System.out.println("Error Sintactico No Recuperable-> Lexema: " +s.value+" Linea: "+s.right+ " Columna: "+s.left);
     cadena += "<tr>"
                + "<td><center><b>" + s.value + "</b></center></td>"
                + "<td><center><b>" + s.right + "</b></center></td>"
                + "<td><center><b>" + s.left+ "</b></center></td>"
                + "</tr>";
}

:}


action code
{:
//Operaciones_Listas operaciones = new Operaciones_Listas();

:}


/*-------- Terminales ---------------*/


terminal String dimv, abreLlave, cierraLlave,abrePar, cierraPar, dosPuntos, puntoComa,si,sino,caso,defecto,selecciona,para,
                mientras,detener,imprimir,retornar,funcion,coma,punto,mensaje,documento,obtener,setelemento,listo,modificado,
				cliqueado,observador,mas,menos,multiplicacion,division,potencia,residuo,menor,mayor,menorIgual,mayorIgual,
				igualIgual,distintoA,and,or,not,valBool,conteo,atexto, id, cadena, numero, masMas, menosMenos, comillaSimple,
				cadenaVector,conteoVector, idEtiqueta, grupoEtiqueta,altoEtiqueta,anchoEtiqueta,alineadoEtiqueta,comillaDoble;



non terminal DECLARACION, ASIGNACION, LISTA_EXPRESIONES,POS_VECTOR,CUERPO,SENTENCIAS,SENTENCIA,
			IF,SELECCIONA, PARA, MIENTRAS,DETENER,IMPRIMIR, RETORNAR, LLAMADA, MENSAJE, SI, SINO, CASO, LISTA_CASOS, DEFECTO, CUERPO_SELECCIONA,
			OPERADOR_PARA, FUNCION, LISTA_ID, OBTENER, SET_ELEMENTO, TIPOS_EVENTOS, UNARIO,
			EXP_ARITMETICA, EXP_RELACIONAL, EXP_LOGICA,ELEMENTO_EXPRESION, NUMERO, CADENA, CADENA_VECTOR,
	BOOLEAN, ID, CONTEO_VECTOR, OBSERVADOR, EXPRESION,SIMB_ARITMETICO,SIMB_RELACIONAL,SIMB_LOGICA, DOCUMENTO, EXPRESION2,
	VECTOR_PROPIEDADES,PROP_VECTOR,DATE, DATETIME, INICIO, TERMINAL,ASIGNA_ARREGLO, CUERPO_ARREGLO,LISTA,ELEMENTO,FUNCION2;
	
	
	
     	precedence left or;
		precedence left and;
		precedence left not;
		precedence left igualIgual, distintoA, menorIgual, mayorIgual,mayor, menor;
		precedence left mas, menos;
        precedence left multiplicacion, division,residuo;
        precedence right potencia;
		precedence left masMas, menosMenos;
		precedence left abrePar;
        
	
	start with INICIO;		
	
	
	INICIO::= SENTENCIAS:a{:parser.listaSentencias= (ArrayList<objetoBase>)a; System.out.println(a);:};


DECLARACION::= dimv id:i puntoComa{:Declaracion s = new Declaracion(i); RESULT = s;:}

	| dimv id:a dosPuntos EXPRESION:b puntoComa{: Declaracion s = new Declaracion(a);
												  Asignacion s2 = new Asignacion(a,b);
												  List<objetoBase> lista = new ArrayList<>();
												  lista.add(s);
												  lista.add(s2);
												  DeclaAsig h = new DeclaAsig(lista);
												  RESULT = h;
												  
													:} 
													
	| dimv id:a abreLlave EXPRESION:el cierraLlave puntoComa{: DeclaArreglo simb = new DeclaArreglo(a,el); RESULT= simb;:}
	
	
	| dimv id:a dosPuntos abreLlave LISTA_EXPRESIONES:lista cierraLlave puntoComa
	{:
		List<objetoBase> l = (ArrayList<objetoBase>)lista;
		Numero num = new Numero(l.size());
		DeclaArreglo d= new DeclaArreglo(a, num);
		asignaArreglo c = new asignaArreglo(a,lista);
		List<objetoBase> listado = new ArrayList<>();
		listado.add(d);
		listado.add(c);
		DeclaAsig h = new DeclaAsig(listado);
		RESULT= h;
	:}
	
	
	| dimv id:a abreLlave EXPRESION:el cierraLlave dosPuntos abreLlave LISTA_EXPRESIONES:b cierraLlave puntoComa
	{:
		DeclaArreglo simb = new DeclaArreglo(a,el);
		asignaArreglo c = new asignaArreglo(a,b);
		DeclaArregloNumAsigna g = new DeclaArregloNumAsigna(simb,c);
		RESULT = g;
	
	:};
	
	
ASIGNACION ::= id:a dosPuntos EXPRESION:b puntoComa{:Asignacion g = new Asignacion(a,b); RESULT= g;:}
	| id:a abreLlave EXPRESION:b cierraLlave dosPuntos EXPRESION:c puntoComa{:AsignaPosArreglo h= new AsignaPosArreglo(a,b,c); RESULT= h;:}
	| EXPRESION:a masMas:b puntoComa{:AsignaUnario u =  new AsignaUnario(a,b); RESULT= u;:}
	| EXPRESION:a menosMenos:b puntoComa{:AsignaUnario u =  new AsignaUnario(a,b); RESULT= u;:}
	| id:a dosPuntos abreLlave LISTA_EXPRESIONES:b cierraLlave puntoComa{:asignaArreglo c = new asignaArreglo(a,b); RESULT=c; :};



	
LISTA_EXPRESIONES::= EXPRESION:a{:
									List<objetoBase> elementos= new ArrayList<>();
									elementos.add((objetoBase)a);
									RESULT= elementos;
								:}	
	|LISTA_EXPRESIONES:l coma EXPRESION:a{:
											List<objetoBase>elementos= (ArrayList<objetoBase>)l;
											elementos.add((objetoBase)a);
											RESULT=elementos;
										 :};
	




/* -------- Estructuras de control -----------*/

CUERPO::= abreLlave cierraLlave{:RESULT= new CuerpoEstructuras();:}
	| abreLlave SENTENCIAS:s cierraLlave{:RESULT= new CuerpoEstructuras(s);:};


SENTENCIAS::= SENTENCIA:a{:List<objetoBase> lista = new ArrayList<>();
							lista.add((objetoBase) a);
							RESULT= lista;:}
		| SENTENCIAS:l SENTENCIA:a{:List<objetoBase> lista = (ArrayList<objetoBase>)l;
									lista.add((objetoBase) a);
									RESULT= lista;:};
		
		
SENTENCIA::= IF:a{:RESULT=a;:}
			|DECLARACION:a{:RESULT= a;:}
			|ASIGNACION:a{:RESULT= a;:}
			|SELECCIONA:a{:RESULT=a;:}
			|PARA:a{:RESULT=a;:}
			|SET_ELEMENTO:a{:RESULT=a;:}
			|OBSERVADOR:a{:RESULT=a;:}
			|MIENTRAS:a{:RESULT=a;:}
			|DETENER:a{:RESULT=a;:}
			|IMPRIMIR:a{:RESULT=a;:}
			|RETORNAR:a{:RESULT=a;:}
			|MENSAJE:a{:RESULT=a;:}
			|FUNCION:a{:RESULT=a;:}
			|LLAMADA:a puntoComa{:RESULT=a;:};
			


IF::= si abrePar EXPRESION:exp cierraPar CUERPO:v sino CUERPO:f{:Si s = new Si(exp,v,f); RESULT= s;:}
	| si abrePar EXPRESION:exp cierraPar CUERPO:v {:Si s = new Si(exp,v,new CuerpoEstructuras()); RESULT= s;:};
	

	
CASO::= caso EXPRESION:ex dosPuntos SENTENCIAS:c{:RESULT = new Caso(ex,new CuerpoEstructuras(c));:};

LISTA_CASOS::= CASO:c{:CuerpoSelecciona cuerpo = new CuerpoSelecciona();
						cuerpo.agregarCaso(c);
						RESULT=cuerpo;
						:}
		| LISTA_CASOS:lista CASO:c{:CuerpoSelecciona cuerpo =(CuerpoSelecciona)lista;
						cuerpo.agregarCaso(c);
						RESULT=cuerpo;
						:};
		
DEFECTO::= defecto dosPuntos SENTENCIAS:c{:RESULT= new CuerpoEstructuras(c);:};

CUERPO_SELECCIONA::= LISTA_CASOS:a{:RESULT=a;:}
	|LISTA_CASOS:a DEFECTO:d{:CuerpoSelecciona cuerpo = (CuerpoSelecciona)a;
							  cuerpo.agregarDefecto(d);
							  RESULT=cuerpo;:}
	|DEFECTO:c{:CuerpoSelecciona cuerpo = new CuerpoSelecciona();
						cuerpo.agregarDefecto(c);
						RESULT=cuerpo;
						:};

SELECCIONA::= selecciona abrePar EXPRESION:e cierraPar abreLlave cierraLlave{:RESULT= new Selecciona(e, new CuerpoSelecciona());:}
	| selecciona abrePar EXPRESION:e cierraPar abreLlave CUERPO_SELECCIONA:c cierraLlave{:RESULT= new Selecciona(e,c);:};
	

OPERADOR_PARA::= masMas{:RESULT="++";:}
		|menosMenos{:RESULT="--";:};
		
PARA::= para abrePar id:a dosPuntos EXPRESION:b puntoComa EXPRESION:exp puntoComa OPERADOR_PARA:op cierraPar CUERPO:c
{: 												  Declaracion s = new Declaracion(a);
												  Asignacion s2 = new Asignacion(a,b);
												  List<objetoBase> lista = new ArrayList<>();
												  lista.add(s);
												  lista.add(s2);
												  DeclaAsig h = new DeclaAsig(lista);
												  AsignaUnario u =  new AsignaUnario(new Id(a),op);
												  RESULT = new Para(h,exp,op,c,u);
												  
:}; 


MIENTRAS::= mientras abrePar EXPRESION:e cierraPar CUERPO:c{:RESULT= new Mientras(e,c);:};

DETENER::= detener puntoComa{:Detener d = new Detener(); RESULT= d;:};

IMPRIMIR::= imprimir abrePar EXPRESION:a cierraPar puntoComa{:Imprimir g = new Imprimir(a); RESULT=g;:};

RETORNAR::= retornar EXPRESION:ex puntoComa{:RESULT= new Retornar(ex);:};

FUNCION::= funcion id:a abrePar LISTA_ID:b cierraPar CUERPO:c{:Funcion f= new Funcion(a,b,c);
												RESULT=f;:}

	| funcion id:i abrePar cierraPar CUERPO:c{:Funcion f= new Funcion(i,new ArrayList<String>(),c);
												RESULT=f;:};

												
FUNCION2::=  funcion abrePar cierraPar CUERPO:c{:Funcion f= new Funcion("",new ArrayList<String>(),c);
												RESULT=f;:};

LISTA_ID::= id:i{:List<String> lista = new ArrayList<>();
				  lista.add(i.toString());
				  RESULT=lista;
				:}
	| LISTA_ID:l coma id:i{:List<String> lista = (ArrayList<String>)l;;
				  lista.add(i.toString());
				  RESULT=lista;
				:};


	
MENSAJE::= mensaje abrePar EXPRESION:a cierraPar puntoComa{:Mensaje j = new Mensaje(a); RESULT= j;:};


/*Expresino*/

ELEMENTO::= id:a{:RESULT= new Id(a);:} 
	|OBTENER:a{:RESULT=a;:};

TIPOS_EVENTOS::= listo{:RESULT="listo";:}
	| modificado{:RESULT="modificado";:}
	|cliqueado{:RESULT="cliqueado";:};
	

OBTENER::= documento punto obtener abrePar cadena:c cierraPar{:RESULT = new Obtener(c);:};
	
SET_ELEMENTO::= ELEMENTO:a punto setelemento abrePar cadena:b coma cadena:c cierraPar puntoComa{:RESULT= new setElemento(a,b,c);:};
	

	
OBSERVADOR::= ELEMENTO:a punto observador abrePar TIPOS_EVENTOS:b coma FUNCION2:c cierraPar puntoComa{:RESULT= new Observador(a,b,c,2);:}
		| documento punto observador abrePar TIPOS_EVENTOS:b coma FUNCION2:c cierraPar puntoComa{:RESULT= new Observador(new objetoBase(),b,c,1);:}
		| ELEMENTO:a punto observador abrePar TIPOS_EVENTOS:b coma LLAMADA:c cierraPar puntoComa{:RESULT= new Observador(a,b,c,2);:}
		| documento punto observador abrePar TIPOS_EVENTOS:b coma LLAMADA:c cierraPar puntoComa{:RESULT= new Observador(new objetoBase(),b,c,1);:};
		

	
LLAMADA::= id:i abrePar LISTA_EXPRESIONES:b cierraPar{: RESULT= new Llamada(i,b);:}
	| id:i abrePar cierraPar{: RESULT= new Llamada(i,new ArrayList<objetoBase>());:};

UNARIO::= EXPRESION:ex masMas{:RESULT= new Unario(ex,"++");:}
		| EXPRESION:ex menosMenos{:RESULT= new Unario(ex,"--");:};
	
	
SIMB_ARITMETICO::= mas:a{:RESULT= "+";:}
	|menos:a{:RESULT= "-";:}
	|multiplicacion:a{:RESULT= "*";:}
	|division:a{:RESULT= "/";:}
	|potencia:a{:RESULT= "^";:}
	|residuo:a{:RESULT= "%";:};
	
SIMB_RELACIONAL::= menor:a{:RESULT= "<";:}
		| mayor:a{:RESULT= ">";:}
		| menorIgual:a{:RESULT= "<=";:}
		|mayorIgual{:RESULT= ">=";:}
		|distintoA{:RESULT= "!=";:}
		|igualIgual{:RESULT= "==";:};
		
SIMB_LOGICA::= and{:RESULT="&&";:}
	|or{:RESULT="||";:};


 
EXPRESION::= EXPRESION:a SIMB_ARITMETICO:b  EXPRESION:c{:Aritmetica ar= new Aritmetica(a,b,c); RESULT= ar;:}
	| EXPRESION:a SIMB_RELACIONAL:b  EXPRESION:c{:Relacional ar= new Relacional(a,b,c); RESULT= ar;:}
	| EXPRESION:a SIMB_LOGICA:b  EXPRESION:c{:Logica ar= new Logica(a,b,c); RESULT= ar;:}
	| not EXPRESION:a{:NOT n = new NOT(a); RESULT=n;:}
	| OBTENER:a{:RESULT=a;:}
	| UNARIO:a{:RESULT=a;:}
    | id:a{:Id i= new Id(a); RESULT= i;:}
    | POS_VECTOR:a{:RESULT=a;:}
	| numero:a{:Numero n = new Numero(a); RESULT= n;:}
	| BOOLEAN:a{:RESULT= a;:}
	| cadena:a{:Cadena cad = new Cadena(a); RESULT = cad;:}
	| conteoVector:a{:RESULT= new Conteo(a);:}
	| cadenaVector:a abrePar cierraPar{:RESULT= new aTexto(a);:}
	| abrePar EXPRESION:a cierraPar{:RESULT=a;:}
	| menos EXPRESION:a{:Negativo g= new Negativo(a); RESULT= g;:}
	| comillaSimple DATE:a comillaSimple{:RESULT = a;:}
	| comillaSimple DATETIME:a comillaSimple{:RESULT = a;:}
	| LLAMADA:a{:RESULT=a;:};



POS_VECTOR::= id:i abreLlave EXPRESION:exp cierraLlave{:RESULT= new posVector(i,exp);:};
 
DATE::=  numero:a division numero:b division numero:c{:Datee d = new Datee(a,b,c); RESULT=d;:};

DATETIME::= DATE:a numero:b dosPuntos numero:c dosPuntos numero:d{:DateTime dt = new DateTime(a,b,c,d); RESULT= dt;:};

BOOLEAN::= valBool:a{:Booleano b = new Booleano(a);RESULT= b;:};




