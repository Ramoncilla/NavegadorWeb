
package proyecto1_201122872.AnalizadorCHTML;

import proyecto1_201122872.AnalizadorCHTML.simbolosCHTML;
import java_cup.runtime.*;
import java.io.*;
import java.lang.*;
import java.util.*;
import java.util.ArrayList;
import proyecto1_201122872.CHTML.ElemetoPropiedad.*;


parser code
{:
 /* -------------------------==: DECLARAR EL ARBOL :==--------------------------*/
    //public Nodo Gramaticas;     
    public static String cadena;

/* -------------------------==: ERROR SINTACTICO :==--------------------------*/
public void syntax_error(Symbol s){
    System.out.println("Error Sintactico -> lexema: " +s.value+" Linea: " +s.right+ " Columna: "+s.left);
    cadena += "<tr>"
                + "<td><center><b>" + s.value + "</b></center></td>"
                + "<td><center><b>" + s.right + "</b></center></td>"
                + "<td><center><b>" + s.left+ "</b></center></td>"
                + "</tr>";
}

/* ------------------==: ERROR SINTACTICO NO RECUPERABLE :==------------------*/
public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
    System.out.println("Error Sintactico No Recuperable-> Lexema: " +s.value+" Linea: "+s.right+ " Columna: "+s.left);
     cadena += "<tr>"
                + "<td><center><b>" + s.value + "</b></center></td>"
                + "<td><center><b>" + s.right + "</b></center></td>"
                + "<td><center><b>" + s.left+ "</b></center></td>"
                + "</tr>";
}

:}


action code
{:
//Operaciones_Listas operaciones = new Operaciones_Listas();

:}

/* -----------------------==: Simbolos Terminales :==------------------------*/







	terminal String menor, mayor, cadena2;

	terminal String chtml, finChtml, encabezado, finEncabezado,cjs, finCjs,ccss,finCcss,cuerpo,finCuerpo, 
		titulo,finTitulo, panel,finPanel, texto,finTexto,imagen,finImagen,boton,finBoton,enlace,finEnlace,tabla,finTabla,fil_t,finFil_t,cb,
		finCb,ct,finCt,texto_a,finTexto_a,caja_texto,finCaja_texto,caja,finCaja,opcion,finOpcion,spinner,finSpinner,saltoFin,id,grupo,alto,
		ancho,alineado,valor,click,ruta,fondo,izquierda,derecha,centrado,igualSimb,puntoComaSimb,identificador,cadena,numero;



	non terminal INICIO;

	non terminal 
		encabezadoProduccion, tituloHeader,cssHeader,jsHeader,cuerpoHeader,elementoHeader;

	non terminal 
		idPropiedad, grupoPropiedad, altoPropiedad, anchoPropiedad,alineadoPropiedad,ccssPropiedad,rutaPropiedad, clickPropiedad,fondoPropiedad,valorPropiedad, propiedadGeneral, listaPropiedadesGenerales;


	non terminal
		tagEncabezado,tagFinEncabezado,
		tagChtml, tagFinChtml,
		tagCjs, tagFinCjs, 
		tagCcss, tagFinCcss, 
		tagTitulo, tagFinTitulo,
		tagCuerpo, tagFinCuerpo, 
		tagTexto, tagFinTexto,
		tagImagen, tagFinImagen,
		tagBoton, tagFinBoton,
		tagEnlace, tagFinEnlace,
		tagTabla, tagFinTabla,
		tagCt, tagFinCt,
		tagCb, tagFinCb,
		tagFil_t, tagFinFil_t,
		tagTexto_a, tagFinTexto_a,
		tagCaja_texto, tagFinCaja_texto,
		tagOpcion, tagFinOpcion,
		tagCaja, tagFinCaja,
		tagSpinner, tagFinSpinner,
		tagPanel, tagFinPanel,
		tagSaltoFin
		;
		
	non terminal listaPropiedadBoton,propiedadCuerpo,listaPropiedadesCuerpo, 
		listaPropiedadeesCajaOpciones, listaPropiedadesEnlace, propiedadBoton, propiedadCajaOpciones,
		propiedadOpcion, listaPropiedadesOpcion, propiedadEnlace;


	non terminal CHTML,ENCABEZADO, CCJS, CCSS, TITULO,CUERPO,TEXTO, IMAGEN,BOTON,ENLACE,TEXTO_A,CAJA_TEXTO,CAJA,SPINNER,SALTO,OPCION,
		listaOpciones,sentenciaCuerpo, cuerpoCuerpo, PANEL,TABLA, LISTA_FILA,FILA,ELEMENTO_FILA,CELDA_ENCABEZADO,CELDA,sentenciaCelda,cuerpoCelda,LISTA_TABLA;


	non terminal cuerpoCHTML;
	


	start with INICIO;
			
			
	INICIO::= CHTML;

	/*---------- propiedades ------------------- */

	rutaPropiedad::= ruta igualSimb cadena:a puntoComaSimb{:propiedadRuta r= new propiedadRuta(a); 
															RESULT=r;
														  :};
   
    clickPropiedad::= click igualSimb cadena:a puntoComaSimb{:propiedadClick r= new propiedadClick(a); 
															RESULT=r;
														  :};
	
	fondoPropiedad::= fondo igualSimb cadena:a puntoComaSimb{:propiedadFondo r= new propiedadFondo(a); 
															RESULT=r;
														  :};
	
	valorPropiedad::= valor igualSimb cadena:a puntoComaSimb{:propiedadValor r= new propiedadValor(a); 
															RESULT=r;
														  :};

	idPropiedad::= id igualSimb cadena:a puntoComaSimb{:propiedadId r= new propiedadId(a); 
															RESULT=r;
														  :};
														  
	grupoPropiedad::= grupo igualSimb cadena:a puntoComaSimb{:propiedadGrupo r= new propiedadGrupo(a); 
															RESULT=r;
														  :};
														  
	altoPropiedad::= alto igualSimb cadena:a puntoComaSimb{:propiedadAlto r= new propiedadAlto(a); 
															RESULT=r;
														  :};
														  
	anchoPropiedad::= ancho igualSimb cadena:a puntoComaSimb{:propiedadAncho r= new propiedadAncho(a); 
															RESULT=r;
														  :};
														  
	alineadoPropiedad::= alineado igualSimb cadena:a puntoComaSimb{:propiedadAlineado r= new propiedadAlineado(a); 
															RESULT=r;
														  :};
														  

	propiedadGeneral::= idPropiedad:a{:RESULT= a;:}
		|grupoPropiedad:a{:RESULT= a;:}
		|altoPropiedad:a{:RESULT= a;:}
		|anchoPropiedad:a{:RESULT= a;:}
		|alineadoPropiedad:a{:RESULT= a;:};

	listaPropiedadesGenerales::= listaPropiedadesGenerales:b propiedadGeneral:a{: List<propiedad> lista = (List<propiedad>)b;
																				lista.add((propiedad)a);
																				RESULT= lista;:} 
		|propiedadGeneral:a {:List<propiedad> propiedades = new ArrayList<>();
							  propiedades.add((propiedad)a);
							  RESULT=propiedades; :};
							  

	propiedadOpcion::= propiedadGeneral:a{:RESULT= a;:}
		|valorPropiedad:a{:RESULT= a;:};
		
	listaPropiedadesOpcion::= listaPropiedadesOpcion:b propiedadOpcion:a{:List<propiedad> lista= (List<propiedad>) b;
																		  lista.add((propiedad)a);
																		  RESULT= lista;:}
		|propiedadOpcion:a{: List<propiedad> lista = new ArrayList<>();
							 lista.add((propiedad)a);
							 RESULT = lista;:};
			
	propiedadCajaOpciones::= propiedadGeneral:a{:RESULT= a;:}
		| clickPropiedad:a{:RESULT= a;:};
		
	listaPropiedadeesCajaOpciones::= listaPropiedadeesCajaOpciones:b propiedadCajaOpciones:a{:List<propiedad> lista= (List<propiedad>) b;
																							  lista.add((propiedad)a);
																							  RESULT= lista;:}
		|propiedadCajaOpciones:a{: List<propiedad> lista = new ArrayList<>();
							 lista.add((propiedad)a);
							 RESULT = lista;:};

	propiedadEnlace::= propiedadGeneral:a{:RESULT= a;:}
		| clickPropiedad:a{:RESULT= a;:}
		| rutaPropiedad:a{:RESULT= a;:};
		
	listaPropiedadesEnlace::= listaPropiedadesEnlace:b propiedadEnlace:a{:List<propiedad> lista= (List<propiedad>) b;
																		  lista.add((propiedad)a);
																		  RESULT= lista;:}
		| propiedadEnlace:a{: List<propiedad> lista = new ArrayList<>();
							 lista.add((propiedad)a);
							 RESULT = lista;:};
		
	propiedadBoton::= propiedadGeneral:a{:RESULT= a;:}
		| clickPropiedad:a{:RESULT= a;:}
		|rutaPropiedad:a{:RESULT= a;:};
		
	listaPropiedadBoton::= listaPropiedadBoton:b propiedadBoton:a{:List<propiedad> lista= (List<propiedad>) b;
																		  lista.add((propiedad)a);
																		  RESULT= lista;:}
	
		| propiedadBoton:a{: List<propiedad> lista = new ArrayList<>();
							 lista.add((propiedad)a);
							 RESULT = lista;:};
		
	
    propiedadCuerpo::= propiedadGeneral:a{:RESULT= a;:}
		| fondoPropiedad:a{:RESULT= a;:};
		
	listaPropiedadesCuerpo::= propiedadCuerpo:a{: List<propiedad> lista = new ArrayList<>();
							 lista.add((propiedad)a);
							 RESULT = lista;:}
							 
		|listaPropiedadesCuerpo:b propiedadCuerpo:a{:List<propiedad> lista= (List<propiedad>) b;
																		  lista.add((propiedad)a);
																		  RESULT= lista;:};
		

		

/*----------------- Tags ----------------------*/


	tagTabla::= menor tabla mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		|menor tabla listaPropiedadesGenerales:b mayor{:List<propiedad> lista= (ArrayList<propiedad>)b;
														RESULT= lista;:};
		
	tagFinTabla::= menor finTabla mayor;
	
	
	tagCt::= menor ct mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
									
		| menor ct listaPropiedadesGenerales :b mayor{:List<propiedad> lista= (ArrayList<propiedad>)b;
														RESULT= lista;:};
		
	tagFinCt::= menor finCt mayor;
	
	tagCb::= menor cb mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		|menor cb listaPropiedadesGenerales:b mayor{:List<propiedad> lista= (ArrayList<propiedad>)b;
														RESULT= lista;:};
		
	tagFinCb::= menor finCb mayor;
	
	tagFil_t::= menor fil_t listaPropiedadesGenerales :b mayor{:List<propiedad> lista= (ArrayList<propiedad>)b;
														RESULT= lista;:}
		| menor fil_t mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:};
		
		
	tagFinFil_t::= menor finFil_t mayor;

    tagCjs::= menor cjs mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
									
		| menor cjs rutaPropiedad:rt mayor{: List<propiedad> lista = new ArrayList<>();
											lista.add((propiedad)rt);
											RESULT= lista;:}; 
		
	tagFinCjs::= menor finCjs mayor;
	
	tagCcss::= menor ccss mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		| menor ccss rutaPropiedad :rt mayor{: List<propiedad> lista = new ArrayList<>();
											lista.add((propiedad)rt);
											RESULT= lista;:}; 
		
	tagFinCcss::= menor finCcss mayor;
	
	
	tagCuerpo::= menor cuerpo mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		|menor cuerpo listaPropiedadesCuerpo:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};
		
	tagFinCuerpo::= menor finCuerpo mayor;
	
	tagTitulo::= menor titulo mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		| menor titulo listaPropiedadesGenerales:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};
		
	tagFinTitulo::= menor finTitulo mayor;

	tagTexto::= menor texto mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		| menor texto listaPropiedadesGenerales :b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};
		
	tagFinTexto::= menor finTexto mayor;
	
	tagPanel::= menor panel mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		| menor panel listaPropiedadesGenerales:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};

	tagFinPanel::= menor finPanel mayor;
	
	
  tagImagen::= menor imagen mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
	| menor imagen listaPropiedadBoton:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};
	
	tagFinImagen::= menor finImagen mayor;
  
  tagBoton::= menor boton listaPropiedadBoton:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:}
	| menor boton  mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:};
	
	tagFinBoton::= menor finBoton mayor;
  
  tagEnlace::= menor enlace mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
	| menor enlace listaPropiedadesEnlace:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};
	
	tagFinEnlace::= menor finEnlace mayor;
	
	
  

  tagTexto_a::= menor texto_a listaPropiedadesGenerales:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:}
	| menor texto_a mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:};
	
	
	tagFinTexto_a::= menor finTexto_a mayor;


    tagCaja_texto::= menor caja_texto mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		|menor caja_texto listaPropiedadesGenerales:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};

	tagFinCaja_texto::= menor finCaja_texto mayor;
	
	
	
	tagCaja::= menor caja listaPropiedadeesCajaOpciones:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:}
		|menor caja mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:};
		
	tagFinCaja::= menor finCaja mayor;
	
	
	tagOpcion::= menor opcion mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		| menor opcion listaPropiedadesOpcion:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};

	tagFinOpcion::= menor finOpcion mayor;
	
	
    tagSaltoFin::= menor saltoFin mayor;
	
	tagSpinner::= menor spinner listaPropiedadesGenerales:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:}
		|menor spinner mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:};
	
	tagFinSpinner::= menor finSpinner mayor;
	
	
	tagEncabezado::= menor encabezado mayor{:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		|menor encabezado listaPropiedadesGenerales:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};

	tagFinEncabezado::= menor finEncabezado mayor;

	tagChtml::= menor chtml mayor {:List<propiedad> lista = new ArrayList<>();
									RESULT= lista;:}
		|menor chtml listaPropiedadesGenerales:b mayor{:List<propiedad> lista = (ArrayList<propiedad>)b;
														RESULT = lista;:};

	tagFinChtml::= menor finChtml mayor;
	
	
/*------------------------- Etiquetas ----------------------------------*/

	CHTML::= tagChtml cuerpoCHTML tagFinChtml;
	
	
	cuerpoCHTML::= ENCABEZADO CUERPO;
	
	
	
	/*---------------- Encabezado -------------------------*/
	elementoHeader::= CCJS
		|CCSS
		|TITULO;
		
	cuerpoHeader::= cuerpoHeader elementoHeader
		| elementoHeader;
		
	ENCABEZADO::= tagEncabezado cuerpoHeader tagFinEncabezado
		| tagEncabezado tagFinEncabezado;
		
	
	CCJS::= tagCjs tagFinCjs;
	CCSS::= tagCcss tagFinCcss;
	TITULO::= tagTitulo cadena2 tagFinTitulo
		|tagTitulo tagFinTitulo;
	
	
	/*---------------------- Cuerpo --------------*/
	
	CUERPO::= tagCuerpo  tagFinCuerpo
		|tagCuerpo  cuerpoCuerpo tagFinCuerpo;
		
	
	TEXTO::= tagTexto cadena2 tagFinTexto
		| tagTexto tagFinTexto;
		
	IMAGEN::= tagImagen cadena tagFinImagen
		| tagImagen tagFinImagen;
		
	BOTON::= tagBoton cadena2 tagFinBoton
		| tagBoton tagFinBoton;
		
	ENLACE::= tagEnlace cadena2 tagFinEnlace
		| tagEnlace tagFinEnlace;
		
	TEXTO_A::= tagTexto_a cadena2 tagFinTexto_a
		| tagTexto_a tagFinTexto_a;
		
	CAJA_TEXTO::= tagCaja_texto cadena2 tagFinCaja_texto
		|tagCaja_texto tagFinCaja_texto;
		
	OPCION::= tagOpcion cadena2 tagFinOpcion
		| tagOpcion tagFinOpcion;
		
	listaOpciones::= listaOpciones OPCION
		|OPCION;
		
	CAJA::= tagCaja listaOpciones tagFinCaja
		| tagCaja tagFinCaja;
		
	SPINNER::= tagSpinner cadena2 tagFinSpinner
		| tagSpinner tagFinSpinner;
		
	SALTO::= tagSaltoFin;
	
	
	
	PANEL::= tagPanel cuerpoCuerpo tagFinPanel
		| tagPanel tagFinPanel;
		

		
	CELDA::= tagCt cuerpoCelda tagFinCt
		| tagCt tagFinCt;
		
	CELDA_ENCABEZADO::= tagCb cuerpoCelda tagFinCb
		| tagCb tagFinCb;
		
	ELEMENTO_FILA::= CELDA
		|CELDA_ENCABEZADO;
		
	LISTA_FILA::= LISTA_FILA ELEMENTO_FILA
		|ELEMENTO_FILA;
		
	FILA ::= tagFil_t LISTA_FILA tagFinFil_t
		| tagFil_t tagFinFil_t;
		
	LISTA_TABLA::= LISTA_TABLA FILA
		|FILA;
		
	
	TABLA::= tagTabla LISTA_TABLA tagFinTabla
		| tagTabla tagFinTabla;
		
		
		
	
	sentenciaCuerpo::= TEXTO
		|IMAGEN
		|BOTON
		|ENLACE
		|TEXTO_A
		|CAJA_TEXTO
		|CAJA
		|SPINNER
		|SALTO
		|PANEL
		|TABLA;
		
	cuerpoCuerpo::= cuerpoCuerpo sentenciaCuerpo
		|sentenciaCuerpo;
		
	sentenciaCelda::= BOTON
		|IMAGEN
		|cadena2;
		
	cuerpoCelda::= cuerpoCelda sentenciaCelda
		|sentenciaCelda;
		
	
	
		
		
	
		
		
	
		
	












    

